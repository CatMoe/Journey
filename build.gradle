import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
}

group = 'net.miaomoe'
version = '1.0'
def adventureVersion = "4.17.0"

repositories {
    gradlePluginPortal()
    mavenCentral()
    mavenLocal()
    maven { url "https://s01.oss.sonatype.org/content/repositories/snapshots/" }
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url "https://repo.viaversion.com" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
}

dependencies {
    // lombok
    compileOnly("org.jetbrains:annotations:26.0.1")
    compileOnly("org.projectlombok:lombok:1.18.34")
    annotationProcessor("org.projectlombok:lombok:1.18.34")
    compileOnly("org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT") // Install this server jar from build tool.
    compileOnly("net.kyori:adventure-api:$adventureVersion")
    compileOnly("net.kyori:adventure-text-minimessage:$adventureVersion")
    compileOnly("net.kyori:adventure-text-serializer-legacy:$adventureVersion")
    compileOnly("net.kyori:adventure-text-serializer-plain:$adventureVersion")
    compileOnly("net.kyori:adventure-platform-bukkit:4.3.4")
    implementation("com.alessiodp.libby:libby-bukkit:2.0.0-SNAPSHOT")
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation true
    archiveClassifier.set("all")
    relocate("com.alessiodp.libby", "net.miaomoe.journey.libby")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
